# We have a conf and classes directory, append to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a recipes directory, add to BBFILES
BBFILES += " \
	${LAYERDIR}/recipes*/*/*.bb \
	${LAYERDIR}/recipes*/*/*.bbappend \
	${LAYERDIR}/recipes*/*/*/*.bb \
	${LAYERDIR}/recipes*/*/*/*.bbappend \
"

BBFILE_COLLECTIONS += "meta-retro"
BBFILE_PATTERN_meta-retro := "^${LAYERDIR}/"
BBFILE_PRIORITY_meta-retro = "100"

LAYERSERIES_COMPAT_meta-retro = "sumo thud warrior zeus"

# Additional license directories.
LICENSE_PATH += "${LAYERDIR}/files/custom-licenses"

LAYERDEPENDS_meta-retro = " \
    core \
    multimedia-layer \
    networking-layer \
    openembedded-layer \
    qt5-layer \
"

LAYERRECOMMENDS_meta-retro = " \
    intel \
    meta-kodi \
    raspberrypi \
"

# The dynamic-layers directory hosts the extensions and layer specific
# modifications.
#
# The .bbappend and .bb files are included if the respective layer
# collection is available.

BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bbappend' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bb' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"

BBFILES_DYNAMIC += " \
    raspberrypi:${LAYERDIR}/dynamic-layers/raspberrypi/*/*/*.bb \
    raspberrypi:${LAYERDIR}/dynamic-layers/raspberrypi/*/*/*.bbappend \
    qt5-layer:${LAYERDIR}/dynamic-layers/qt5-layer/*/*/*.bb \
    qt5-layer:${LAYERDIR}/dynamic-layers/qt5-layer/*/*/*.bbappend \
"
