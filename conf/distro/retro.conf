require conf/distro/poky.conf

DISTRO = "retro"
DISTRO_NAME = "Retro"

MAINTAINER = "Bart≈Çomiej Burdukiewicz <bartlomiej.burdukiewicz@gmail.com>"

RPI_ALWAYS_FULLHD ??= "1"
RPI_ENABLE_AUDIO ??= "1"

DISTRO_FEATURES_BACKFILL_CONSIDERED += "sysvinit"
VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = "systemd-compat-units"

RETRO_ADDITIONAL_MULTIMEDIA_PACKAGES ?= ""

RETRO_ADDITIONAL_MESA_GPUS ?= " \
  etnaviv \
  freedreno \
  gallium \
  gallium-llvm \
  i915 \
  iris \
  kmsro \
  lima \
  nouveau \
  panfrost \
  r300 \
  r600 \
  radeonsi \
  v3d \
  vc4 \
"

PACKAGECONFIG_append_pn-mesa = "${RETRO_ADDITIONAL_MESA_GPUS}"

RETRO_DISTRIBUTION_FEATURES ??= " \
  alsa \
  pam \
  polkit \
  pulseaudio \
  rauc \
  retroarch-automount \
  retroarch-firmware \
  retroarch-gles \
  retroarch-gles3 \
  systemd \
  vulkan \
"

DISTRO_FEATURES_append = " \
  ${RETRO_DISTRIBUTION_FEATURES} \
"

RETRO_DISABLE_WAYLAND ?= "1"
RETRO_DISABLE_X11 ?= "1"

DISTRO_FEATURES_remove = "${@bb.utils.contains('RETRO_DISABLE_WAYLAND', '1', 'wayland', '', d)}"
DISTRO_FEATURES_remove = "${@bb.utils.contains('RETRO_DISABLE_X11', '1', 'x11', '', d)}"

RETROARCH_PREFERRED_KERNEL_FOR_X86 ?= "linux-yocto"

PREFERRED_PROVIDER_virtual/kernel_x86 = "${RETROARCH_PREFERRED_KERNEL_FOR_X86}"
PREFERRED_PROVIDER_virtual/kernel_x86-64 = "${RETROARCH_PREFERRED_KERNEL_FOR_X86}"

DISTROOVERRIDES =. "${@bb.utils.contains('DISTRO_FEATURES', 'rauc', 'rauc-integration:', '', d)}"

SYSTEMD_DEFAULT_TARGET = "multi-user.target"

SANITY_TESTED_DISTROS_append = "gentoo \n"
