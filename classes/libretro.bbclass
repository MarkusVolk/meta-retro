LIBRETRO_CORE ??= ""

PR = "r101"

LIBRETRO_PLATFORM_AUTODETECT = "unix"

LIBRETRO_PLATFORM_AUTODETECT_RPI1 ??= "rpi1"
LIBRETRO_PLATFORM_AUTODETECT_RPI2 ??= "rpi2"
LIBRETRO_PLATFORM_AUTODETECT_RPI3 ??= "rpi3"
LIBRETRO_PLATFORM_AUTODETECT_RPI4 ??= "rpi4"

# LIBRETRO_PLATFORM_AUTODETECT_raspberrypi = "${LIBRETRO_PLATFORM_AUTODETECT_RPI1}"
# LIBRETRO_PLATFORM_AUTODETECT_raspberrypi-cm = "${LIBRETRO_PLATFORM_AUTODETECT_RPI1}"
# LIBRETRO_PLATFORM_AUTODETECT_raspberrypi0 =  "${LIBRETRO_PLATFORM_AUTODETECT_RPI1}"
# LIBRETRO_PLATFORM_AUTODETECT_raspberrypi0-wifi = "${LIBRETRO_PLATFORM_AUTODETECT_RPI1}"
#
# LIBRETRO_PLATFORM_AUTODETECT_raspberrypi2 = "${LIBRETRO_PLATFORM_AUTODETECT_RPI2}"
#
# LIBRETRO_PLATFORM_AUTODETECT_raspberrypi-cm3 = "${LIBRETRO_PLATFORM_AUTODETECT_RPI3}"
# LIBRETRO_PLATFORM_AUTODETECT_raspberrypi3 = "${LIBRETRO_PLATFORM_AUTODETECT_RPI3}"
# LIBRETRO_PLATFORM_AUTODETECT_raspberrypi3-64 = "${LIBRETRO_PLATFORM_AUTODETECT_RPI3}"
#
# LIBRETRO_PLATFORM_AUTODETECT_raspberrypi4 = "${LIBRETRO_PLATFORM_AUTODETECT_RPI4}"
# LIBRETRO_PLATFORM_AUTODETECT_raspberrypi4-64 = "${LIBRETRO_PLATFORM_AUTODETECT_RPI4}"

LIBRETRO_PLATFORM ??= "${LIBRETRO_PLATFORM_AUTODETECT}"
LIBRETRO_EXTRA_FLAGS ??= ""

FILES_${PN} += "${libdir}/libretro"

LIBRETRO_CPU_ARCH_DETECT = "${TUNE_ARCH}"
LIBRETRO_CPU_ARCH_DETECT_aarch64 = "arm64"
LIBRETRO_CPU_ARCH_DETECT_arm = "arm"
LIBRETRO_CPU_ARCH_DETECT_x86 = "x86"
LIBRETRO_CPU_ARCH_DETECT_x86-64 = "x86-64"

LIBRETRO_CPU_ARCH ??= "${LIBRETRO_CPU_ARCH_DETECT}"

do_compile() {
  MAKEFILE_PATH="Makefile";
  [[ -f "Makefile.libretro" ]] && MAKEFILE_PATH="Makefile.libretro"

  export CXXFLAGS="${CXXFLAGS} -fPIC -shared"
  export CFLAGS="${CFLAGS} -fPIC -shared"

  oe_runmake -f "${MAKEFILE_PATH}" \
    ${LIBRETRO_EXTRA_FLAGS} \
    CC_PREFIX=${TARGET_PREFIX} \
    CC_AS="${CC}" \
    ASFLAGS="${TARGET_CC_ARCH} -shared -fPIC -c -frename-registers -fno-strict-aliasing -ffast-math -ftree-vectorize" \
    platform=${LIBRETRO_PLATFORM} \
    LIBRETRO_CPU=${LIBRETRO_CPU_ARCH} \
    ARCH=${LIBRETRO_CPU_ARCH} \
    HAVE_NEON="${@bb.utils.contains('TUNE_FEATURES', 'neon', '1', '0', d)}" \
    ARM="${IS_ARM_ARCH}" \
    CPU_ARCH=${TUNE_ARCH} \
    LDFLAGS_END="${LDFLAGS}"

#  LDFLAGS_END hack found in flycast
}

do_install() {
  install -d ${D}${libdir}/libretro
  install -m 644 ${S}/${LIBRETRO_CORE}_libretro.so ${D}${libdir}/libretro/${LIBRETRO_CORE}_libretro.so
}
